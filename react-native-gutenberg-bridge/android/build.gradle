
buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.wordpress-mobile.gutenberg-mobile'

// fallback flag value for when lib is compiled individually (e.g. via jitpack)
project.ext.buildGutenbergFromSource = false

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    google()
    jcenter()
    if (!rootProject.ext.buildGutenbergFromSource) {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "https://unpkg.com/react-native@0.57.5/android"
        }
        maven {
            // Local Maven repo containing AARs with JSC library built for Android
            url "https://unpkg.com/jsc-android@224109.1.0/dist/"
        }
    } else {
        // RN maven repo is configured by the parent project, which is wpandroid
    }
    maven { url "https://jitpack.io" }
}

dependencies {
    if (rootProject.ext.buildGutenbergFromSource) {
        println "using gutenberg from source"
        implementation project(':react-native-svg')
        implementation project(':react-native-aztec')
        implementation project(':react-native-recyclerview-list')
    } else {
        implementation ('com.github.wordpress-mobile:react-native-svg:faed05be8dca2b0df8957c5f72c40eccef12144d')
        implementation ('com.github.wordpress-mobile:react-native-aztec:67cbd7188d0b2014739aadf39035e3a92bf5b029')
        implementation ('com.github.wordpress-mobile:react-native-recyclerview-list:a2e8ca550412504c32218fd473c55f696f18f1f7')
    }
    implementation 'com.facebook.react:react-native:+'
}
